// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zero the x input
    Mux16(a=x, b=false, sel=zx, out=zxOut);

    // negate the x input
    Not16(in=zxOut, out=zxNotOut);
    Mux16(a=zxOut, b=zxNotOut, sel=nx, out=nxOut);

    // zero the y input
    Mux16(a=y, b=false, sel=zy, out=zyOut);

    // negate the y input
    Not16(in=zyOut, out=zyNotOut);
    Mux16(a=zyOut, b=zyNotOut, sel=ny, out=nyOut);
    
    // fOut = (f == 1) ? (x + y) : (x & y)
    And16(a=nxOut, b=nyOut, out=nxOutAndnyOut);
    Add16(a=nxOut, b=nyOut, out=nxOutAddnyOut);
    Mux16(a=nxOutAndnyOut, b=nxOutAddnyOut, sel=f, out=fOut);
    
    // negate fOut if no == 1
    // capture the lowest 8 bits of out, the highest 8 bits, and the MSB
    Not16(in=fOut, out=fNotOut);
    Mux16(a=fOut, b=fNotOut, sel=no, out=out, out[0..7]=outLow8, out[8..15]=outHigh8, out[15]=outMSB);

    // zr = (out == 0) ? 1 : 0
    Or8Way(in=outLow8, out=low8OrOut);
    Or8Way(in=outHigh8, out=high8OrOut);
    Or(a=low8OrOut, b=high8OrOut, out=outOrOut);
    Not(in=outOrOut, out=zr);

    // ng = (out < 0) ? 1 : 0
    And(a=true, b=outMSB, out=ng);
}
